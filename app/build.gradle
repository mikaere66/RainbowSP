plugins {
    id 'com.android.application'
    id 'com.google.devtools.ksp'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt' // Hilt
    // kotlin-kapt should be last, to avoid: The following options were not recognized by any processor: '[kapt.kotlin.generated, dagger.fastInit]'
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "com.michaelrmossman.kotlin.rainbows"
        minSdk 24 // Nougat 7.0
        targetSdk 34
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        compose true
        dataBinding true
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    composeOptions {
        kotlinCompilerExtensionVersion = compose_version // "1.5.3"
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    namespace 'com.michaelrmossman.kotlin.rainbows'
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0' // Was 1.9.0
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // Extensions
    implementation "androidx.activity:activity-ktx:$activity_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    // THIRD-PARTY

    // https://github.com/Stericson/RootTools
    implementation files('libs/RootTools-4.2.jar')

    // https://github.com/jaredrummler/AndroidDeviceNames
    implementation 'com.jaredrummler:android-device-names:2.1.1'

    // https://android-arsenal.com/details/1/6589
    implementation ('com.github.Binary-Finery:FabToast:1.0') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }

    // https://github.com/coil-kt/coil (Image Loading)
    implementation("io.coil-kt:coil:2.4.0")

    // https://android-arsenal.com/details/1/7294
    implementation ('com.github.ngallazzi:HighlightingView:1.1.0') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }

    // https://android-arsenal.com/details/1/8352
    implementation "androidx.activity:activity-compose:$activity_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation ('com.stephenvinouze:SegmentedProgressBar:1.0.0') {
        exclude group: 'androidx.activity',         module: 'activity-compose'
        exclude group: 'androidx.lifecycle',        module: 'androidx.lifecycle-ktx'
        exclude group: 'androidx.compose.material', module: 'material'
        exclude group: 'androidx.compose.ui',       module: 'ui'
        exclude group: 'androidx.compose.ui',       module: 'ui-tooling-preview'
    }

    // https://github.com/nex3z/ToggleButtonGroup
    implementation ('com.nex3z:toggle-button-group:1.2.3') {
        exclude group: 'androidx.appcompat', module: 'appcompat'
    }

    // HARD-CODED
    // RainLayoutView
    // https://android-arsenal.com/details/1/7864
}